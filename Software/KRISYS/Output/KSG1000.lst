              	; --------------------------------------
              	; zasm: assemble "KSG1000.asm"
              	; date: 2024-08-16 00:13:17
              	; --------------------------------------


              	;
              	;********************************************************************
              	;*
              	;*        K R I S Y S   S G 1 0 0 0   C O R E
              	;*
              	;********************************************************************
              	
              	#include "KRISYS.asm"
              	;
              	;********************************************************************
              	;*
              	;*              I D E N T I T Y   K R I S Y S
              	;*
              	;*      The [K]lunkly [R]emapper / [I]nterpreter [SYS]tem
              	;*
              	;*             Written by Gavin Tersteeg, 2024
              	;*              Copyleft, All Wrongs Reserved
              	;*
              	;*
              	;*   This piece of software allows different classic Z80 systems
              	;*   to run as "virtual machines" on top of existing hardware by
              	;*   use of a ZMM (Z80 MEGAMAPPER). It does this by remapping RAM
              	;*   and I/O address space ot match that of it's target system.
              	;*   Anything that can't be emulated by simple remapping is instead
              	;*   interpreted using I/O traps. 
              	;*
              	;*   This allows virtualized machines to run with an acceptable
              	;*   degree of speed and accuracy. While the ZMM is still quite
              	;*   limited in what sort of hardware can be efficiently virtualized,
              	;*   anything that avoids MMIO or graphics hardware that isn't a VDP
              	;*   generally can be made to work.
              	;* 
              	;********************************************************************
              		
              	; ----------------------------
              	; ******** ZASM Setup ********
              	; ----------------------------
              	
0020:         	stack_size = 0x20
              	
              	#target BIN
0100:         	#code	_TEXT,0x0100	; Setup to run as a CP/M executable
0491:         	#code	_DATA,_TEXT_end
056E:         	#data	_BSS,_DATA_end
              	
              	; Make sure w don't overrun available memory
              	#assert	_BSS_end < (zmm_capture-stack_size)
              	
              	.area	_TEXT
0100: C32D04  		jp	kri_start
              	
              	; -------------------------
              	; ******** Equates ********
              	; -------------------------
              	
              	; CP/M Stuff
0005:         	bdos		equ	0x0005
0000:         	bdos_exit	equ	0x00
0001:         	bdos_con_in	equ	0x01
0002:         	bdos_con_out	equ	0x02
0009:         	bdos_print	equ	0x09
000F:         	bdos_open	equ	0x0F
0014:         	bdos_read	equ	0x14
001A:         	bios_set_dma	equ	0x1A
              	
0080:         	cpm_command	equ	0x0080
              	
              	; Z80 MEGAMAPPER Stuff
0030:         	zmm_bnk0	equ	0x30	; 16K Bank 0 (0x0000 - 0x3FFF)
0031:         	zmm_bnk1	equ	0x31	; 16K Bank 1 (0x4000 - 0x7FFF)
0032:         	zmm_bnk2	equ	0x32	; 16K Bank 2 (0x8000 - 0xBFFF)
0033:         	zmm_bnk3	equ	0x33	; 16K Bank 3 (0xC000 - 0xFFFF)
0034:         	zmm_ctrl	equ	0x34	; ZMM Control Register
0030:         	zmm_isr		equ	0x30	; ZMM Trapped Instruction Register
0032:         	zmm_addr_hi	equ	0x32	; ZMM Trap Address High
0033:         	zmm_addr_lo	equ	0x33	; ZMM Trap Address Low
0037:         	zmm_trap	equ	0x37 	; ZMM Trap Vector
              	
7000:         	zmm_capture	equ	0x7000
8000:         	zmm_map		equ	0x8000
C000:         	zmm_top		equ	0xC000
              	
0070:         	zmm_capt_set	equ	0b01110000
007F:         	zmm_capt_res	equ	0b01111111
              	
              	; General Z80 Stuff
0066:         	nmi_address	equ	0x0066
0067:         	nmi_vector	equ	nmi_address+1
              	
              	; NABU Specific Stuff
0000:         	nabu_nctl	equ	0x00	; NABU Control Register
0040:         	nabu_ay_data	equ	0x40	; AY-3-8910 Data Port
0041:         	nabu_at_latch	equ	0x41	; AY-3-8910 Latch Port
              	
              	; -------------------------------------
              	; ******** Additional Includes ********
              	; -------------------------------------
              	
              	#include "MEMORY.asm"
              	;
              	;********************************************************************
              	;*
              	;*                    Z M M   M E M O R Y
              	;* 
              	;*    The ZMM can be configured to have different amounts
              	;*    of memory installed. Instead of dicking around with 
              	;*    on-board jumpers, KRISYS will simply check what banks
              	;*    are available on startup. Memory will be dynamically
              	;*    allocated as needed by the client process.
              	;*
              	;*    Memory can be assigned to specific owners in the code.
              	;*    Valid owner IDs range from 1 to 254. Owner 0 is reserved
              	;*    for unallocatable banks.
              	;*
              	;********************************************************************
              	
              	; ----------------------------
              	; ********  Functions ********
              	; ----------------------------
              	
              	.area	_TEXT
              		
              	; Initalize the memory map
              	; Each bank of the ZMM will be probed, and writable
              	; banks will be recorded on the allocated bank map
              	;
              	; Returns nothing
              	; Uses: AF, BC, DE, HL
0103:         	mem_map_init:
              		; Go to virtual mode
0103: CDCF01  		call	zmm_set_virt
              		
              		; Reset free bank counter
0106: 3E00    		ld	a,0
0108: 326E05  		ld	(banks_free),a
              		
              		; Write tags to all banks
010B: 0600    		ld	b,0
010D: 78      	0$:	ld	a,b
010E: CD3902  		call	zmm_bnk3_set
0111: 3200C0  		ld	(zmm_top),a
0114: ED44    		neg
0116: 3201C0  		ld	(zmm_top+1),a
0119: 04      		inc	b
011A: F20D01  		jp	p,0$
              		
              		; Mark any banks that record correctly
011D: 0600    		ld	b,0
011F: 216F05  		ld	hl,alloc_bank_map
0122: AF      	1$:	xor	a
0123: 77      		ld	(hl),a
0124: 78      		ld	a,b
0125: CD3902  		call	zmm_bnk3_set
0128: 3A00C0  		ld	a,(zmm_top)
012B: B8      		cp	b
012C: C24401  		jp	nz,2$
012F: 3A01C0  		ld	a,(zmm_top+1)
0132: 4F      		ld	c,a
0133: 78      		ld	a,b
0134: ED44    		neg
0136: B9      		cp	c
0137: C24401  		jp	nz,2$
              	
              		; Mark it
013A: 3EFF    		ld	a,0xFF
013C: 77      		ld	(hl),a
013D: 3A6E05  		ld	a,(banks_free)
0140: 3C      		inc	a
0141: 326E05  		ld	(banks_free),a
              		
              		; Next
0144: 23      	2$:	inc	hl
0145: 04      		inc	b
0146: F22201  		jp	p,1$
              		
              		; Disable virtual mode
0149: CDDA01  		call	zmm_set_real
              		
              		; Print out result
014C: 3A6E05  		ld	a,(banks_free)
014F: CD6104  		call	tohex
0152: ED53B604		ld	(str_mem_init_cnt),de
0156: 119104  		ld	de,str_mem_init
0159: CD4904  		call	cpm_print
              		
              		; Do we actually have an acceptable amount of memory?
015C: 3A6E05  		ld	a,(banks_free)
015F: 3D      		dec	a
0160: 3D      		dec	a
0161: FAA301  		jp	m,mem_empty
              		
              		; We do, return
0164: C9      		ret
              		
              	; Allocates a bank of memory
              	; Will produce an error if no banks are available,
              	; check (banks_free) to avoid
              	; D = Owner ID (1-254)
              	;
              	; Returns A = Bank #
              	; Uses: AF, BC, HL
0165:         	mem_alloc:
              		; Check and decrement free memory
0165: 3A6E05  		ld	a,(banks_free)
0168: 3D      		dec	a
0169: 326E05  		ld	(banks_free),a
016C: FAA301  		jp	m,mem_empty
              		
              		; Look for the first free bank
016F: 216F05  		ld	hl,alloc_bank_map
0172: 018000  		ld	bc,0x0080
0175: 3EFF    		ld	a,0xFF
0177: EDB1    		cpir
              		
              		; Make sure we found something
0179: C2A301  		jp	nz,mem_empty
              		
              		; Save and exit
017C: 2B      		dec	hl
017D: 72      		ld	(hl),d
017E: 016F05  		ld	bc,alloc_bank_map
0181: B7      		or	a
0182: ED42    		sbc	hl,bc
0184: 6F      		ld	l,a
0185: C9      		ret
              		
              	; Frees a bank of memory
              	; Safe to use on banks that are not free / not owned
              	; A = Bank #
              	; D = Owner ID (1-254)
              	;
              	; Returns nothing
              	; Uses: AF, BC, HL
0186:         	mem_free:
              		; Find location in memory
0186: 0600    		ld	b,0
0188: 4F      		ld	c,a
0189: 216F05  		ld	hl,alloc_bank_map
018C: 09      		add	hl,bc
              		
              		; Check owner
018D: 7E      		ld	a,(hl)
018E: BA      		cp	d
018F: C0      		ret	nz
              		
              		; Free bank
0190: 3EFF    		ld	a,0xFF
0192: 77      		ld	(hl),a
              		
              		; Increment banks free
0193: 216E05  		ld	hl,banks_free
0196: 34      		inc	(hl)
0197: C9      		ret
              		
              	; Free all banks by owner
              	; D = Owner ID (1-254)
              	;
              	; Returns nothing
              	; Uses: AF, BC, HL
0198:         	mem_free_all:
              		; Free a bank
0198: AF      		xor	a
0199: F5      	0$:	push	af
019A: CD8601  		call	mem_free
019D: F1      		pop	af
              		
              		; Next bank
019E: 3C      		inc	a
019F: F29901  		jp	p,0$
01A2: C9      		ret
              		
              	; Error out if empty
              	;
              	; Does not return
              	; Uses: N/A
01A3:         	mem_empty:
01A3: 11BC04  		ld	de,str_mem_empty
01A6: CD4904  		call	cpm_print
01A9: C35C04  		jp	cpm_exit
              	
              	; -------------------------
              	; ******** Strings ********
              	; -------------------------
              	
              	.area	_DATA
              	
              	; Inital string that prints when the program is started
0491:         	str_mem_init:
0491: 494E4954		defb	'INITIALIZED MEMORY MAP',0x0A,0x0D
0495: 49414C49	
0499: 5A454420	
049D: 4D454D4F	
04A1: 5259204D	
04A5: 41500A0D	
04A9: 42414E4B		defb	'BANK COUNT = '
04AD: 20434F55	
04B1: 4E54203D	
04B5: 20      	
04B6:         	str_mem_init_cnt:
04B6: 5858480A		defb	'XXH',0x0A,0x0D,'$'
04BA: 0D24    	
              		
04BC:         	str_mem_empty:
04BC: 494E5355		defb	'INSUFFICIENT MEMORY',0x0A,0x0D,'$'
04C0: 46464943	
04C4: 49454E54	
04C8: 204D454D	
04CC: 4F52590A	
04D0: 0D24    	
              	
              	; ---------------------------
              	; ******** Variables ********
              	; ---------------------------
              	
              	.area	_BSS
              	
              	; Free bank count
056E:         	banks_free:
056E: 00      		defs	1
              	
              	; Allocated bank map
              	; This 128 byte table keeps track of every single 16K bank
              	; that exists on the ZMM. Populated on startup
056F:         	alloc_bank_map:
056F: 00000000		defs	128
0573: 00...   	
              	#include "ZMM.asm"
              	;
              	;********************************************************************
              	;*
              	;*                  Z M M   M A N A G E M E N T
              	;* 
              	;*    These routines are used to manage the state of the ZMM.
              	;*    This includes the setting of the registers as well as setting
              	;*    up trap and interrupt stuff.
              	;*
              	;********************************************************************
              	
              	; ----------------------------
              	; ********  Functions ********
              	; ----------------------------
              	
              	.area	_TEXT
              	
              	; Initalize ZMM and reset registers
              	;
              	; Returns nothing
              	; Uses: AF
01AC:         	zmm_init:
              		; Zero out control register
01AC: 3E00    		ld	a,0
01AE: 32EF05  		ld	(zmm_ctrl_state),a
01B1: 32F005  		ld	(zmm_bnk0_state),a
01B4: 32F105  		ld	(zmm_bnk1_state),a
01B7: 32F205  		ld	(zmm_bnk2_state),a
01BA: 32F305  		ld	(zmm_bnk3_state),a
              		
              		; See if we can swing in and out of virtual mode
01BD: CDCF01  		call	zmm_set_virt
01C0: CDDA01  		call	zmm_set_real
              		
              		; Tell the user that the ZMM is read
01C3: 11D204  		ld	de,str_zmm_init
01C6: C34904  		jp	cpm_print
              		
              	; Set the ZMM control register to the recorded state
              	; (zmm_ctrl_state) = New value of ZMM control register
              	;
              	; Returns nothing
              	; Uses: AF
01C9:         	zmm_ctrl_set:
01C9: 3AEF05  		ld	a,(zmm_ctrl_state)
01CC: D334    		out	(zmm_ctrl),a
01CE: C9      		ret
              		
              	; Go to virtual mode
              	;
              	; Returns nothing
              	; Uses: AF
01CF:         	zmm_set_virt:
01CF: 3AEF05  		ld	a,(zmm_ctrl_state)
01D2: F601    		or	0b00000001
01D4: 32EF05  		ld	(zmm_ctrl_state),a
01D7: D334    		out	(zmm_ctrl),a
01D9: C9      		ret
              		
              	; Go to real mode
              	;
              	; Returns nothing
              	; Uses: AF
01DA:         	zmm_set_real:
01DA: 3AEF05  		ld	a,(zmm_ctrl_state)
01DD: E6FE    		and	0b11111110
01DF: 32EF05  		ld	(zmm_ctrl_state),a
01E2: D334    		out	(zmm_ctrl),a
01E4: C9      		ret
              		
              	; Set program direction to "IN"
              	;
              	; Returns nothing
              	; Uses: AF
01E5:         	zmm_prgm_in:
01E5: 3AEF05  		ld	a,(zmm_ctrl_state)
01E8: F602    		or	0b00000010
01EA: 32EF05  		ld	(zmm_ctrl_state),a
01ED: D334    		out	(zmm_ctrl),a
01EF: C9      		ret
              		
              	; Set program direction to "OUT"
              	;
              	; Returns nothing
              	; Uses: AF
01F0:         	zmm_prgm_out:
01F0: 3AEF05  		ld	a,(zmm_ctrl_state)
01F3: E6FD    		and	0b11111101
01F5: 32EF05  		ld	(zmm_ctrl_state),a
01F8: D334    		out	(zmm_ctrl),a
01FA: C9      		ret
              		
              	; Turn on irq intercept mode
              	;
              	; Returns nothing
              	; Uses: AF
01FB:         	zmm_irq_inter:
01FB: 3AEF05  		ld	a,(zmm_ctrl_state)
01FE: F604    		or	0b00000100
0200: 32EF05  		ld	(zmm_ctrl_state),a
0203: D334    		out	(zmm_ctrl),a
0205: C9      		ret
              		
              	; Turn off irq intercept mode
              	;
              	; Returns nothing
              	; Uses: AF
0206:         	zmm_irq_normal:
0206: 3AEF05  		ld	a,(zmm_ctrl_state)
0209: E6FB    		and	0b11111011
020B: 32EF05  		ld	(zmm_ctrl_state),a
020E: D334    		out	(zmm_ctrl),a
0210: C9      		ret
              	
              	; Turn on force virtual irq
              	;
              	; Returns nothing
              	; Uses: AF
0211:         	zmm_irq_on:
0211: 3AEF05  		ld	a,(zmm_ctrl_state)
0214: F608    		or	0b00001000
0216: 32EF05  		ld	(zmm_ctrl_state),a
0219: D334    		out	(zmm_ctrl),a
021B: C9      		ret
              		
              	; Turn off force virtual irq
              	;
              	; Returns nothing
              	; Uses: AF
021C:         	zmm_irq_off:
021C: 3AEF05  		ld	a,(zmm_ctrl_state)
021F: E6F7    		and	0b11110111
0221: 32EF05  		ld	(zmm_ctrl_state),a
0224: D334    		out	(zmm_ctrl),a
0226: C9      		ret
              		
              	; Set bank 0
              	; A = Bank to set
              	;
              	; Returns nothing
              	; Uses: AF, B
0227:         	zmm_bnk0_set:
0227: 32F005  		ld	(zmm_bnk0_state),a
022A: D330    		out	(zmm_bnk0),a
022C: C9      		ret
              		
              	; Set bank 1
              	; A = Bank to set
              	;
              	; Returns nothing
              	; Uses:  AF, B
022D:         	zmm_bnk1_set:
022D: 32F105  		ld	(zmm_bnk1_state),a
0230: D331    		out	(zmm_bnk1),a
0232: C9      		ret
              		
              	; Set bank 2
              	; A = Bank to set
              	;
              	; Returns nothing
              	; Uses:  AF, B
0233:         	zmm_bnk2_set:
0233: 32F205  		ld	(zmm_bnk2_state),a
0236: D332    		out	(zmm_bnk2),a
0238: C9      		ret
              		
              	; Set bank 3
              	; A = Bank to set
              	;
              	; Returns nothing
              	; Uses:  AF, B
0239:         	zmm_bnk3_set:
0239: 32F305  		ld	(zmm_bnk3_state),a
023C: D333    		out	(zmm_bnk3),a
023E: C9      		ret
              		
              	; -------------------------
              	; ******** Strings ********
              	; -------------------------
              	
              	.area	_DATA
              	
              	; Inital string that prints when the program is started
04D2:         	str_zmm_init:
04D2: 494E4954		defb	'INITIALIZED ZMM',0x0A,0x0D,'$'
04D6: 49414C49	
04DA: 5A454420	
04DE: 5A4D4D0A	
04E2: 0D24    	
              		
              	; ---------------------------
              	; ******** Variables ********
              	; ---------------------------
              	
              	.area	_BSS
              	
              	; Reflected state of control register
05EF:         	zmm_ctrl_state:
05EF: 00      		defs	1
              		
              	; Bank 0 state
05F0:         	zmm_bnk0_state:
05F0: 00      		defs	1
              		
              	; Bank 1 state
05F1:         	zmm_bnk1_state:
05F1: 00      		defs	1
              		
              	; Bank 2 state
05F2:         	zmm_bnk2_state:
05F2: 00      		defs	1
              		
              	; Bank 3 state
05F3:         	zmm_bnk3_state:
05F3: 00      		defs	1
              		
              	#include "RESOURCE.asm"
              	;
              	;********************************************************************
              	;*
              	;*               R E S O U R C E   M A N A G E M E N T
              	;* 
              	;*    These routines handle obtaining use-supplied resources
              	;*    such as configurations, ROM images, and storage bindings.
              	;*    During startup, these resources will be loaded to build
              	;*    the virtual machine.
              	;*
              	;********************************************************************
              	
              	; -------------------------
              	; ******** Equates ********
              	; -------------------------
              	
0010:         	arg_size	equ 16
              	
              	; ----------------------------
              	; ********  Functions ********
              	; ----------------------------
              	
              	.area	_TEXT
              	
              	; Initalize resources
              	;
              	; Returns nothing
              	; Uses: AF, BC, HL
023F:         	res_init:
              		; Start by zero-terminating string
023F: 218000  		ld	hl,cpm_command
0242: 4E      		ld	c,(hl)
0243: 0600    		ld	b,0
0245: 09      		add	hl,bc
0246: 23      		inc	hl
0247: 70      		ld	(hl),b
              		
0248: C9      		ret
              		
              	; Loads an open resource into a bankmap
              	; Bankmap will be zero-padded to fill requested number of records
              	; HL = Address of bankmap
              	; BC = Number of records (128 byte blocks) to load
              	;
              	; Returns nothing
              	; Uses: all
0249:         	res_load:
              	
              		; Set read flag
0249: 3EFF    		ld	a,0xFF
024B: 329806  		ld	(res_do_read),a
              	
              		; Save args
024E: 229906  		ld	(res_bankmap),hl
0251: ED439606		ld	(res_sectors),bc
              	
              		; Virtual mode should be off while we do this
0255: 3AEF05  		ld	a,(zmm_ctrl_state)
0258: F5      		push	af
0259: CDDA01  		call	zmm_set_real
              		
              		; Do function call
025C: CD6B02  		call	0$
              		
              		; Reset bank 3
025F: 3AF305  		ld	a,(zmm_bnk3_state)
0262: D333    		out	(zmm_bnk3),a
              		
              		; Restore register
0264: F1      		pop	af
0265: 32EF05  		ld	(zmm_ctrl_state),a
0268: C3C901  		jp	zmm_ctrl_set
              		
              		; Allocate a new bank
026B: CD6501  	0$:	call	mem_alloc
026E: 2A9906  		ld	hl,(res_bankmap)
0271: 77      		ld	(hl),a
0272: 23      		inc	hl
0273: 229906  		ld	(res_bankmap),hl
              		
              		; Set the bank in slot 3
0276: D333    		out	(zmm_bnk3),a
              		
              		; Set DMA address
0278: 111606  		ld	de,res_buffer
027B: 0E1A    		ld	c,bios_set_dma
027D: CD0500  		call	bdos
              		
              		; Set pointer	
0280: 2100C0  		ld	hl,zmm_top
0283: 229B06  		ld	(res_pointer),hl
              		
              		; We will be loading up to 128 records at a time here
0286: 0680    		ld	b,128
              		
              		; Do read operation
0288: C5      	1$:	push	bc
0289: 119D06  		ld	de,res_fcb
028C: 0E14    		ld	c,bdos_read
028E: 3A9806  		ld	a,(res_do_read)
0291: B7      		or	a
0292: C40500  		call	nz,bdos
              		
              		; Check file I/O result
0295: B7      		or	a
0296: C4BF02  		call	nz,50$
              		
              		; Go to virtual mode
0299: CDCF01  		call	zmm_set_virt
              		
              		; Copy record to bank
029C: 211606  		ld	hl,res_buffer
029F: ED5B9B06		ld	de,(res_pointer)
02A3: 018000  		ld	bc,128
02A6: EDB0    		ldir
02A8: ED539B06		ld	(res_pointer),de
              		
              		; Back to real mode
02AC: CDDA01  		call	zmm_set_real 
              		
              		; Get BC back to restore sector counter
02AF: C1      		pop	bc
              		
              		; Decrement record count
02B0: 2A9606  		ld	hl,(res_sectors)
02B3: 2B      		dec	hl
02B4: 229606  		ld	(res_sectors),hl
02B7: 7C      		ld	a,h
02B8: B5      		or	l
02B9: C8      		ret	z
              		
              		; Go get another sector
02BA: 10CC    		djnz	1$
02BC: C36B02  		jp	0$
              	
              		; Zero buffer and reset read flag
02BF: AF      	50$:	xor	a
02C0: 329806  		ld	(res_do_read),a
              		
02C3: 211606  		ld	hl,res_buffer
02C6: 111706  		ld	de,res_buffer+1
02C9: 017F00  		ld	bc,128-1
02CC: 77      		ld	(hl),a
02CD: EDB0    		ldir
              	
02CF: C9      		ret
              		
              	; Opens a file based on the resource argument
              	; If the file cannot be opened, an error will be thrown
              	; (res_argument) = File to open
              	;
              	; Returns nothing
              	; Uses: all
02D0:         	res_open:
              	
              		; Virtual mode should be off while we do this
02D0: 3AEF05  		ld	a,(zmm_ctrl_state)
02D3: F5      		push	af
02D4: CDDA01  		call	zmm_set_real
              		
              		; Do function call
02D7: CDE102  		call	0$
              		
              		; Restore register
02DA: F1      		pop	af
02DB: 32EF05  		ld	(zmm_ctrl_state),a
02DE: C3C901  		jp	zmm_ctrl_set
              		
              		; Let the user know we are loading stuff
02E1: 0E09    	0$:	ld	c,bdos_print
02E3: 11E404  		ld	de,str_load_a
02E6: CD0500  		call	bdos
              		
              		; Print resource name
02E9: 210506  		ld	hl,res_current
02EC: 5E      		ld	e,(hl)
02ED: 23      		inc	hl
02EE: 56      		ld	d,(hl)
02EF: CDB503  		call	res_printzt
              		
              		; Next string
02F2: 0E09    		ld	c,bdos_print
02F4: 11ED04  		ld	de,str_load_b
02F7: CD0500  		call	bdos
              		
              		; Print file name
02FA: 11F405  		ld	de,res_argument
02FD: CDB503  		call	res_printzt
              		
              		; CRLF
0300: 0E09    		ld	c,bdos_print
0302: 116705  		ld	de,str_crlf
0305: CD0500  		call	bdos
              		
              		; Detect if there is an argument
0308: 3AF405  		ld	a,(res_argument)
030B: B7      		or	a
030C: C21A03  		jp	nz,1$
              		
              		; No argument, error!
030F: 0E09    		ld	c,bdos_print
0311: 11F404  		ld	de,str_arg_empty
0314: CD0500  		call	bdos
0317: C35C04  		jp	cpm_exit
              	
              		; Reset fields
031A: AF      	1$:	xor	a
031B: 219D06  		ld	hl,res_fcb
031E: 119E06  		ld	de,res_fcb+1
0321: 012300  		ld	bc ,36-1
0324: 77      		ld	(hl),a
0325: EDB0    		ldir
              		
0327: 3E20    		ld	a,0x20
0329: 219E06  		ld	hl,res_fcb_name
032C: 119F06  		ld	de,res_fcb_name+1
032F: 010A00  		ld	bc ,11-1
0332: 77      		ld	(hl),a
0333: EDB0    		ldir
              	
              		; Is there a prefix?
0335: 21F405  		ld	hl,res_argument
0338: 3AF505  		ld	a,(res_argument+1)
033B: FE3A    		cp	':'
033D: C24F03  		jp	nz,2$
              		
              		; Set prefix
0340: 3AF405  		ld	a,(res_argument)
0343: D640    		sub	'A'-1
0345: FE11    		cp	17
0347: D2AA03  		jp	nc,99$
034A: 329D06  		ld	(res_fcb_drive),a
034D: 23      		inc	hl
034E: 23      		inc	hl
              	
              		; HL = Proper filename start
034F: 0608    	2$:	ld	b,8
0351: 119E06  		ld	de,res_fcb_name
              		
              		; Copy it over
0354: 7E      	3$:	ld	a,(hl)
0355: B7      		or	a
0356: CAAA03  		jp	z,99$
0359: FE2A    		cp	'*'
035B: CA6B03  		jp	z,4$
035E: FE2E    		cp	'.'
0360: CA7203  		jp	z,5$
0363: 12      		ld	(de),a
0364: 13      		inc	de
0365: 23      		inc	hl
0366: 10EC    		djnz	3$
0368: C37203  		jp	5$
              	
              		; Fill remains of FCB file name
036B: 3E3F    	4$:	ld	a,'?'
036D: 12      		ld	(de),a
036E: 13      		inc	de
036F: 10FA    		djnz	4$
0371: 23      		inc	hl
              	
              		; We should either see a '.' or a null character
0372: 7E      	5$:	ld	a,(hl)
0373: B7      		or	a
0374: CA9B03  		jp	z,8$
0377: FE2E    		cp	'.'
0379: C2AA03  		jp	nz,99$
037C: 23      		inc	hl
              		
              		; Fill in extension
037D: 0603    		ld	b,3
037F: 11A606  		ld	de,res_fcb_type
              		
              		; Copy it over
0382: 7E      	6$:	ld	a,(hl)
0383: B7      		or	a
0384: CA9B03  		jp	z,8$
0387: FE2A    		cp	'*'
0389: CA9403  		jp	z,7$
038C: 12      		ld	(de),a
038D: 13      		inc	de
038E: 23      		inc	hl
038F: 10F1    		djnz	6$
0391: C39B03  		jp	8$
              		
              		; Fill remains of FCB file extension
0394: 3E3F    	7$:	ld	a,'?'
0396: 12      		ld	(de),a
0397: 13      		inc	de
0398: 10D1    		djnz	4$
039A: 23      		inc	hl
              	
              		; We should get a zero
039B: 7E      	8$:	ld 	a,(hl)
039C: B7      		or	a
039D: C2AA03  		jp	nz,99$
              		
              		; It is filled in, attempt to open
03A0: 0E0F    		ld	c,bdos_open
03A2: 119D06  		ld	de,res_fcb
03A5: CD0500  		call	bdos
              		
              		; Check error
03A8: 3C      		inc	a
03A9: C0      		ret	nz
              		
              		; Error!
03AA: 0E09    	99$:	ld	c,bdos_print
03AC: 110B05  		ld	de,str_arg_fail
03AF: CD0500  		call	bdos
03B2: C35C04  		jp	cpm_exit
              		
              		
              	; Print a zero terminated string
              	; We should be in real mode for this
              	; DE = String
              	;
              	; Returns nothing
              	; Uses: All
03B5:         	res_printzt:
03B5: 1A      	0$:	ld	a,(de)
03B6: B7      		or	a
03B7: C8      		ret	z
              		
              		; Print character
03B8: D5      		push	de
03B9: 5F      		ld	e,a
03BA: 0E02    		ld	c,bdos_con_out
03BC: CD0500  		call	bdos
03BF: D1      		pop	de
03C0: 13      		inc	de
03C1: C3B503  		jp	0$
              	
              	; Find a resource from the command line
              	; If the resource is found, the contents will be cached in memory
              	; DE = Name of resource (upper case only) 
              	;
              	; Returns A = 0xFF if no resource is found
              	; Uses: AF, BC, DE, HL
03C4:         	res_locate:
              		; Save resource
03C4: ED530506		ld	(res_current),de
              	
              		; Travel to the start of arguments
03C8: 218100  		ld	hl,cpm_command+1
03CB: 7E      	0$:	ld	a,(hl)
03CC: B7      		or	a
03CD: CA2A04  		jp	z,99$
03D0: FE21    		cp	0x21
03D2: D2D903  		jp	nc,1$
03D5: 23      		inc	hl
03D6: C3CB03  		jp	0$
              		
              		; Found an argument
              		; Check it against the contents of (DE)
              		; Also must start with '-'
03D9: FE2D    	1$:	cp	'-'
03DB: C2EC03  		jp	nz,3$
03DE: 23      		inc	hl
03DF: D5      		push	de
03E0: 1A      	2$:	ld	a,(de)
              		
              		; Check if at end of string
03E1: B7      		or	a
03E2: CAFA03  		jp	z,4$
              		
              		; No? Well lets see if (de) = (hl)
03E5: BE      		cp	(hl)
03E6: 23      		inc	hl
03E7: 13      		inc	de
03E8: CAE003  		jp	z,2$
              		
              		; Strings are different!
              		; Escape from the current argument and continue
03EB: D1      		pop	de
03EC: 7E      	3$:	ld	a,(hl)
03ED: B7      		or	a
03EE: CA2A04  		jp	z,99$
03F1: FE21    		cp	0x21
03F3: DACB03  		jp	c,0$
03F6: 23      		inc	hl
03F7: C3EC03  		jp	3$
              	
              		; Make sure we are at the end of the argument as well
03FA: D1      	4$: 	pop	de
03FB: 7E      		ld	a,(hl)
03FC: FE21    		cp	0x21
03FE: D2EC03  		jp	nc,3$
              		
              		; Ok, lets copy the argument into memory if it exists
0401: 11F405  		ld	de,res_argument
0404: AF      		xor	a
0405: 12      		ld	(de),a
              		
              		; Travel to the start of the argument
0406: 7E      	5$:	ld	a,(hl)
0407: B7      		or	a
0408: CA2804  		jp	z,89$
040B: FE21    		cp	0x21
040D: D21404  		jp	nc,6$
0410: 23      		inc	hl
0411: C30604  		jp	5$
              	
              		; Make sure it doesn't start with '-'
0414: FE2D    	6$:	cp	'-'
0416: CA2804  		jp	z,89$
              		
              		; Ok, lets copy up 16 bytes of this
0419: 0610    		ld	b,arg_size
041B: 7E      	7$:	ld	a,(hl)
041C: FE21    		cp	0x21
041E: DA2604  		jp	c,8$
0421: 12      		ld	(de),a
0422: 23      		inc	hl
0423: 13      		inc	de
0424: 10F5    		djnz	7$
              	
              		; Zero terminate
0426: AF      	8$:	xor	a
0427: 12      		ld	(de),a
              		
              		; Good ending
0428: AF      	89$:	xor	a
0429: C9      		ret
              	
              		; Bad ending
042A: 3EFF    	99$:	ld	a,0xFF
042C: C9      		ret
              		
              	; -------------------------
              	; ******** Strings ********
              	; -------------------------
              	
              	.area	_DATA
              	
              	; Loading string components
04E4:         	str_load_a:
04E4: 4C4F4144		defb	'LOADING $'
04E8: 494E4720	
04EC: 24      	
              		
04ED:         	str_load_b:
04ED: 2046524F		defb	' FROM $'
04F1: 4D2024  	
              		
              	; Error messages
04F4:         	str_arg_empty:
04F4: 4E4F2041		defb	'NO ARGUMENT PROVIDED',0x0A,0x0D,'$'
04F8: 5247554D	
04FC: 454E5420	
0500: 50524F56	
0504: 49444544	
0508: 0A0D24  	
              		
050B:         	str_arg_fail:
050B: 4641494C		defb	'FAILED TO OPEN FILE',0x0A,0x0D,'$'
050F: 45442054	
0513: 4F204F50	
0517: 454E2046	
051B: 494C450A	
051F: 0D24    	
              		
              	; ---------------------------
              	; ******** Variables ********
              	; ---------------------------
              	
              	.area	_BSS
              	
              	; Stores a zero-terminated string for the resource argument
05F4:         	res_argument:
05F4: 00000000		defs	arg_size+1
05F8: 00...   	
              		
              	; Current resource being accessed
0605:         	res_current:
0605: 00000000		defs	arg_size+1
0609: 00...   	
              		
              	; Resource buffer
0616:         	res_buffer:
0616: 00000000		defs	128
061A: 00...   	
              		
              	; Sector counter
0696:         	res_sectors:
0696: 0000    		defs	2
              		
              	; Do we need to read?
0698:         	res_do_read:
0698: 00      		defs	1
              		
              	; Resource bankmap
0699:         	res_bankmap:
0699: 0000    		defs	2
              		
              	; Loading pointer
069B:         	res_pointer:
069B: 0000    		defs	2
              		
              	; File control block for use in loading resources
069D:         	res_fcb:
069D: 00000000		defs	36
06A1: 00...   	
069D:         	res_fcb_drive	equ	res_fcb
069E:         	res_fcb_name	equ	res_fcb+1
06A6:         	res_fcb_type	equ	res_fcb+9
06A9:         	res_fcb_ex	equ	res_fcb+12
06AA:         	res_fcb_s1	equ	res_fcb+13
06AB:         	res_fcb_s2	equ	res_fcb+14
06AC:         	res_fcb_rc	equ	res_fcb+15
06AD:         	res_fcb_data	equ	res_fcb+16
06BD:         	res_fcb_cr	equ	res_fcb+32
06BE:         	res_fcb_r0	equ	res_fcb+33
06BF:         	res_fcb_r1	equ	res_fcb+34
06C0:         	res_fcb_r2	equ	res_fcb+35
              	
              	; --------------------------------
              	; ******** KRISYS Startup ********
              	; --------------------------------
              	
              	.area	_TEXT
              		
              		; KRISYS entry point
042D:         	kri_start:	
              		; Set up stack
042D: F3      		di
042E: 310070  		ld	sp,zmm_capture
0431: 215C04  		ld	hl,cpm_exit
0434: E5      		push	hl
              		
              		; Print "hello" splash
0435: 0E09    		ld	c,bdos_print
0437: 112105  		ld	de,str_splash
043A: CD0500  		call	bdos
              		
              		; Initalize subcomponents
043D: CDAC01  		call	zmm_init
0440: CD0301  		call	mem_map_init
0443: CD3F02  		call	res_init
              		
              		
              		; Start the core
0446: C37804  		jp	core_start
              		
              	; ------------------------------
              	; ******** CP/M Service ********
              	; ------------------------------
              		
              	; Print something to the CP/M console
              	; DE = Address of string to print
              	;
              	; Returns nothing
              	; Uses: All
0449:         	cpm_print:
              		; Save control register state
0449: 3AEF05  		ld	a,(zmm_ctrl_state)
044C: F5      		push	af
              		
              		; Go to real mode
044D: CDDA01  		call zmm_set_real
              		
              		; Do BDOS call
0450: 0E09    		ld	c,bdos_print
0452: CD0500  		call	bdos
              		
              		; Restore register
0455: F1      		pop	af
0456: 32EF05  		ld	(zmm_ctrl_state),a
0459: C3C901  		jp	zmm_ctrl_set
              		
              	; Go back to CP/M
              	;
              	; Does not return
              	; Uses: N/A
045C:         	cpm_exit:
045C: 0E00    		ld	c,bdos_exit
045E: CD0500  		call	bdos	
              		
              	; ----------------------
              	; ******** Misc ********
              	; ----------------------
              		
              	; Converts the value into an 8 bit hex number
              	; A = Number to convert
              	;
              	; Returns DE = result
              	; Uses: AF, DE
0461: 57      	tohex:	ld	d,a
0462: CD6C04  		call	0$
0465: 5F      		ld	e,a
0466: 7A      		ld	a,d
0467: CD7004  		call	1$
046A: 57      		ld	d,a
046B: C9      		ret
              		
046C: 1F      	0$:	rra
046D: 1F      		rra
046E: 1F      		rra
046F: 1F      		rra
0470: F6F0    	1$:	or	0xF0
0472: 27      		daa
0473: C6A0    		add	a,0xA0
0475: CE40    		adc	a,0x40
0477: C9      		ret
              	
              	; -------------------------
              	; ******** Strings ********
              	; -------------------------
              	
              	.area	_DATA
              	
              	; Inital string that prints when the program is started
0521:         	str_splash:
0521: 4944454E		defb	'IDENTITY KRISYS HYPERVISOR, CP/M EDT.',0x0A,0x0D
0525: 54495459	
0529: 204B5249	
052D: 53595320	
0531: 48595045	
0535: 52564953	
0539: 4F522C20	
053D: 43502F4D	
0541: 20454454	
0545: 2E0A0D  	
0548: 5645522E		defb	'VER. 0.0.1, GAVIN TERSTEEG 2024'
054C: 20302E30	
0550: 2E312C20	
0554: 47415649	
0558: 4E205445	
055C: 52535445	
0560: 45472032	
0564: 303234  	
              		
              	; Carriage return, line break
0567:         	str_crlf:
0567: 0A0D24  		defb	0x0A,0x0D,'$' 
              		
              	
              	; ---------------------------
              	; ******** Core Init ********
              	; ---------------------------
              	
              	.area	_TEXT
              	
              	; Start of SG-1000 core
0478:         	core_start:
              	
              		; Try to find rom resource
0478: 116A05  		ld	de,str_rom
047B: CDC403  		call	res_locate
047E: B7      		or	a
047F: C25C04  		jp	nz,cpm_exit
              		
              		; Open the resource
0482: CDD002  		call	res_open
              		
              		; Load resources into bankmap
0485: 21C106  		ld	hl,bm_rom
0488: 010001  		ld	bc,256
048B: CD4902  		call	res_load
              	
048E: C35C04  		jp	cpm_exit
              		
              	; -------------------------
              	; ******** Strings ********
              	; -------------------------
              	
              	.area	_DATA
              	
              	; Resource strings
056A:         	str_rom:
056A: 524F4D00		defb	'ROM',0
              		
              		
              	; ---------------------------
              	; ******** Variables ********
              	; ---------------------------
              	
              	.area	_BSS
              	
              	; Reflected state of control register
06C1:         	bm_rom:
06C1: 0000    		defs	2


; +++ segments +++

#CODE _TEXT    = $0100 =   256,  size = $0391 =   913
#CODE _DATA    = $0491 =  1169,  size = $00DD =   221
#DATA _BSS     = $056E =  1390,  size = $0155 =   341

; +++ global symbols +++

_BSS             = $056E =  1390  _BSS    KRISYS.asm:28 (unused)
_BSS_end         = $06C3 =  1731  _BSS    KRISYS.asm:28
_BSS_size        = $0155 =   341  _BSS    KRISYS.asm:28 (unused)
_DATA            = $0491 =  1169  _DATA   KRISYS.asm:27 (unused)
_DATA_end        = $056E =  1390  _DATA   KRISYS.asm:27
_DATA_size       = $00DD =   221  _DATA   KRISYS.asm:27 (unused)
_TEXT            = $0100 =   256  _TEXT   KRISYS.asm:26 (unused)
_TEXT_end        = $0491 =  1169  _TEXT   KRISYS.asm:26
_TEXT_size       = $0391 =   913  _TEXT   KRISYS.asm:26 (unused)
alloc_bank_map   = $056F =  1391  _BSS    MEMORY.asm:211
arg_size         = $0010 =    16  _BSS    RESOURCE.asm:17
banks_free       = $056E =  1390  _BSS    MEMORY.asm:205
bdos             = $0005 =     5  _TEXT   KRISYS.asm:49
bdos_con_in      = $0001 =     1  _TEXT   KRISYS.asm:51 (unused)
bdos_con_out     = $0002 =     2  _TEXT   KRISYS.asm:52
bdos_exit        = $0000 =     0  _TEXT   KRISYS.asm:50
bdos_open        = $000F =    15  _TEXT   KRISYS.asm:54
bdos_print       = $0009 =     9  _TEXT   KRISYS.asm:53
bdos_read        = $0014 =    20  _TEXT   KRISYS.asm:55
bios_set_dma     = $001A =    26  _TEXT   KRISYS.asm:56
bm_rom           = $06C1 =  1729  _BSS    KSG1000.asm:53
core_start       = $0478 =  1144  _TEXT   KSG1000.asm:17
cpm_command      = $0080 =   128  _TEXT   KRISYS.asm:58
cpm_exit         = $045C =  1116  _TEXT   KRISYS.asm:153
cpm_print        = $0449 =  1097  _TEXT   KRISYS.asm:132
kri_start        = $042D =  1069  _TEXT   KRISYS.asm:102
mem_alloc        = $0165 =   357  _TEXT   MEMORY.asm:103
mem_empty        = $01A3 =   419  _TEXT   MEMORY.asm:177
mem_free         = $0186 =   390  _TEXT   MEMORY.asm:135
mem_free_all     = $0198 =   408  _TEXT   MEMORY.asm:161 (unused)
mem_map_init     = $0103 =   259  _TEXT   MEMORY.asm:30
nabu_at_latch    = $0041 =    65  _TEXT   KRISYS.asm:85 (unused)
nabu_ay_data     = $0040 =    64  _TEXT   KRISYS.asm:84 (unused)
nabu_nctl        = $0000 =     0  _TEXT   KRISYS.asm:83 (unused)
nmi_address      = $0066 =   102  _TEXT   KRISYS.asm:79
nmi_vector       = $0067 =   103  _TEXT   KRISYS.asm:80 (unused)
res_argument     = $05F4 =  1524  _BSS    RESOURCE.asm:456
res_bankmap      = $0699 =  1689  _BSS    RESOURCE.asm:476
res_buffer       = $0616 =  1558  _BSS    RESOURCE.asm:464
res_current      = $0605 =  1541  _BSS    RESOURCE.asm:460
res_do_read      = $0698 =  1688  _BSS    RESOURCE.asm:472
res_fcb          = $069D =  1693  _BSS    RESOURCE.asm:484
res_fcb_cr       = $06BD =  1725  _BSS    RESOURCE.asm:494 (unused)
res_fcb_data     = $06AD =  1709  _BSS    RESOURCE.asm:493 (unused)
res_fcb_drive    = $069D =  1693  _BSS    RESOURCE.asm:486
res_fcb_ex       = $06A9 =  1705  _BSS    RESOURCE.asm:489 (unused)
res_fcb_name     = $069E =  1694  _BSS    RESOURCE.asm:487
res_fcb_r0       = $06BE =  1726  _BSS    RESOURCE.asm:495 (unused)
res_fcb_r1       = $06BF =  1727  _BSS    RESOURCE.asm:496 (unused)
res_fcb_r2       = $06C0 =  1728  _BSS    RESOURCE.asm:497 (unused)
res_fcb_rc       = $06AC =  1708  _BSS    RESOURCE.asm:492 (unused)
res_fcb_s1       = $06AA =  1706  _BSS    RESOURCE.asm:490 (unused)
res_fcb_s2       = $06AB =  1707  _BSS    RESOURCE.asm:491 (unused)
res_fcb_type     = $06A6 =  1702  _BSS    RESOURCE.asm:488
res_init         = $023F =   575  _TEXT   RESOURCE.asm:29
res_load         = $0249 =   585  _TEXT   RESOURCE.asm:47
res_locate       = $03C4 =   964  _TEXT   RESOURCE.asm:339
res_open         = $02D0 =   720  _TEXT   RESOURCE.asm:154
res_pointer      = $069B =  1691  _BSS    RESOURCE.asm:480
res_printzt      = $03B5 =   949  _TEXT   RESOURCE.asm:319
res_sectors      = $0696 =  1686  _BSS    RESOURCE.asm:468
stack_size       = $0020 =    32          KRISYS.asm:31
str_arg_empty    = $04F4 =  1268  _DATA   RESOURCE.asm:443
str_arg_fail     = $050B =  1291  _DATA   RESOURCE.asm:446
str_crlf         = $0567 =  1383  _DATA   KRISYS.asm:196
str_load_a       = $04E4 =  1252  _DATA   RESOURCE.asm:436
str_load_b       = $04ED =  1261  _DATA   RESOURCE.asm:439
str_mem_empty    = $04BC =  1212  _DATA   MEMORY.asm:195
str_mem_init     = $0491 =  1169  _DATA   MEMORY.asm:189
str_mem_init_cnt = $04B6 =  1206  _DATA   MEMORY.asm:192
str_rom          = $056A =  1386  _DATA   KSG1000.asm:42
str_splash       = $0521 =  1313  _DATA   KRISYS.asm:191
str_zmm_init     = $04D2 =  1234  _DATA   ZMM.asm:184
tohex            = $0461 =  1121  _TEXT   KRISYS.asm:166
zmm_addr_hi      = $0032 =    50  _TEXT   KRISYS.asm:67 (unused)
zmm_addr_lo      = $0033 =    51  _TEXT   KRISYS.asm:68 (unused)
zmm_bnk0         = $0030 =    48  _TEXT   KRISYS.asm:61
zmm_bnk0_set     = $0227 =   551  _TEXT   ZMM.asm:142 (unused)
zmm_bnk0_state   = $05F0 =  1520  _BSS    ZMM.asm:198
zmm_bnk1         = $0031 =    49  _TEXT   KRISYS.asm:62
zmm_bnk1_set     = $022D =   557  _TEXT   ZMM.asm:152 (unused)
zmm_bnk1_state   = $05F1 =  1521  _BSS    ZMM.asm:202
zmm_bnk2         = $0032 =    50  _TEXT   KRISYS.asm:63
zmm_bnk2_set     = $0233 =   563  _TEXT   ZMM.asm:162 (unused)
zmm_bnk2_state   = $05F2 =  1522  _BSS    ZMM.asm:206
zmm_bnk3         = $0033 =    51  _TEXT   KRISYS.asm:64
zmm_bnk3_set     = $0239 =   569  _TEXT   ZMM.asm:172
zmm_bnk3_state   = $05F3 =  1523  _BSS    ZMM.asm:210
zmm_capt_res     = $007F =   127  _TEXT   KRISYS.asm:76 (unused)
zmm_capt_set     = $0070 =   112  _TEXT   KRISYS.asm:75 (unused)
zmm_capture      = $7000 = 28672  _TEXT   KRISYS.asm:71
zmm_ctrl         = $0034 =    52  _TEXT   KRISYS.asm:65
zmm_ctrl_set     = $01C9 =   457  _TEXT   ZMM.asm:44
zmm_ctrl_state   = $05EF =  1519  _BSS    ZMM.asm:194
zmm_init         = $01AC =   428  _TEXT   ZMM.asm:22
zmm_irq_inter    = $01FB =   507  _TEXT   ZMM.asm:97 (unused)
zmm_irq_normal   = $0206 =   518  _TEXT   ZMM.asm:108 (unused)
zmm_irq_off      = $021C =   540  _TEXT   ZMM.asm:130 (unused)
zmm_irq_on       = $0211 =   529  _TEXT   ZMM.asm:119 (unused)
zmm_isr          = $0030 =    48  _TEXT   KRISYS.asm:66 (unused)
zmm_map          = $8000 = 32768  _TEXT   KRISYS.asm:72 (unused)
zmm_prgm_in      = $01E5 =   485  _TEXT   ZMM.asm:75 (unused)
zmm_prgm_out     = $01F0 =   496  _TEXT   ZMM.asm:86 (unused)
zmm_set_real     = $01DA =   474  _TEXT   ZMM.asm:64
zmm_set_virt     = $01CF =   463  _TEXT   ZMM.asm:53
zmm_top          = $C000 = 49152  _TEXT   KRISYS.asm:73
zmm_trap         = $0037 =    55  _TEXT   KRISYS.asm:69 (unused)


total time: 0.0129 sec.
no errors
